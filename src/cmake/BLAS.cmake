function(hyhound_set_mkl_interface)
    set(MKL_INTERFACE_int32_t "lp64")
    set(MKL_INTERFACE_int64_t "ilp64")
    set(MKL_INTERFACE_int "lp64")
    set(MKL_INTERFACE_long "ilp64")
    set(MKL_INTERFACE_long-long "ilp64")
    set(MKL_INTERFACE_long-long-int "ilp64")
    string(REPLACE " " "-" INDEX_TYPE "${HYHOUND_DENSE_INDEX_TYPE}")
    if (NOT DEFINED MKL_INTERFACE_${INDEX_TYPE})
        message(SEND_ERROR "Unsupported index type ${HYHOUND_DENSE_INDEX_TYPE}")
    endif()
    set(MKL_INTERFACE "${MKL_INTERFACE_${INDEX_TYPE}}" PARENT_SCOPE)
endfunction()

if (HYHOUND_WITH_MKL AND HYHOUND_WITH_OPENBLAS)
    message(WARNING "Options HYHOUND_WITH_MKL and HYHOUND_WITH_OPENBLAS are both enabled. Using MKL and ignoring OpenBLAS.")
endif()

add_library(blas-lapack-lib INTERFACE)
target_compile_definitions(blas-lapack-lib INTERFACE
    $<$<BOOL:${HYHOUND_WITH_MKL}>:HYHOUND_WITH_MKL>)
# MKL
if (HYHOUND_WITH_MKL)
    set(HYHOUND_MKLROOT_DEFINED false)
     if (NOT "${HYHOUND_MKLROOT}" STREQUAL "")
        set(HYHOUND_MKLROOT_DEFINED true)
        string(REPLACE "$ENV{HOME}" "\$ENV{HOME}"
            HYHOUND_MKLROOT_ESCAPED ${HYHOUND_MKLROOT})
    endif()
    if (NOT DEFINED MKL_THREADING)
        if (HYHOUND_WITH_OPENMP)
            if (CMAKE_CXX_COMPILER_ID STREQUAL "GNU")
                set(MKL_THREADING "gnu_thread")
            endif()
        else()
            set(MKL_THREADING "sequential")
        endif()
    endif()
    if (NOT DEFINED MKL_LINK)
        set(MKL_LINK "static")
    endif()
    if (NOT DEFINED MKL_INTERFACE)
        hyhound_set_mkl_interface()
    endif()
    set(HYHOUND_MKLROOT_DEFINED ${HYHOUND_MKLROOT_DEFINED} CACHE INTERNAL "")
    set(HYHOUND_MKLROOT_ESCAPED ${HYHOUND_MKLROOT_ESCAPED} CACHE INTERNAL "")
    if (HYHOUND_MKLROOT_DEFINED AND NOT DEFINED ENV{MKLROOT})
        set(ENV{MKLROOT} "${HYHOUND_MKLROOT}")
    endif()
    if (DEFINED ENV{MKLROOT})
        list(PREPEND CMAKE_FIND_ROOT_PATH "$ENV{MKLROOT}")
    endif()
    find_package(MKL CONFIG REQUIRED)
    target_link_libraries(blas-lapack-lib INTERFACE MKL::MKL)
# OpenBLAS
elseif(HYHOUND_WITH_OPENBLAS)
    find_package(OpenBLAS REQUIRED)
    target_link_libraries(blas-lapack-lib INTERFACE OpenBLAS::OpenBLAS)
# Generic BLAS/LAPACK
else()
    find_package(BLAS REQUIRED)
    find_package(LAPACK REQUIRED)
    target_link_libraries(blas-lapack-lib INTERFACE BLAS::BLAS LAPACK::LAPACK)
endif()
add_library(hyhound::blas-lapack-lib ALIAS blas-lapack-lib)
